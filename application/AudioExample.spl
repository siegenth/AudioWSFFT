namespace application ;

use com.ibm.streamsx.inet.rest::HTTPTupleView ;
use com.ibm.streamsx.inet.wsserver::WebSocketInject ;
use com.ibm.streamsx.inet.rest::WebContext ;
use com.ibm.streamsx.json::JSONToTuple ;
use com.ibm.streamsx.json::TupleToJSON ;
use com.ibm.streamsx.inet.wsserver::WebSocketSend ;
use com.ibm.streams.timeseries.analysis::* ;
use com.ibm.streams.timeseries.analysis::FFT ;
type BinaryDataType = tuple<blob outBlob> ;
type AudioDataType = tuple<rstring id, rstring command, uint64 size,
	blob outBlob, rstring signalData> ;
type AudioSignalType = tuple<rstring id, rstring signalData> ;
type AudioCommandType = tuple<rstring id, rstring command,
	rstring commandData> ;
type AudioSampleType = tuple<rstring id, float32 sample> ;
type AudioSampleListType = tuple<rstring id, list<float64>samples > ;
type AudioSample64Type = tuple<rstring id, float64 sample> ;
type AudioFFTAmpType = tuple<rstring id, list<float64> ampFFT> ;
type AudioTileType = tuple<rstring id, list<float64> tile> ;
type AudioQuantileType = tuple<rstring id, float64 tile1, float64 tile2,
	float64 tile3, float64 tile4> ;
type SimpleTuple = tuple<rstring data> ;
int64 getMilliseconds(timestamp ts)
{
	return(getSeconds(ts) * 1000l) +(int64)(getNanoseconds(ts) / 1000000u) ;
}
/**
 * Convert A 2 character string to binary value.
 * Yes this can be collapsed, but you can see this. 
 */
uint8 hexToInt(list<uint8> cvt)
{
	list<uint8> mapp = [ 0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 0, 0, 0, 0, 0, 0, 0, 10,
		11, 12, 13, 14, 15 ] ;
		//////////////////// 0, 1, 2, 3, 4, 5, 6. 7, 8, 9, :, ;, <, =, >, ?, @,  A, B, C, D, E, F	
	mutable uint8 l, h ;
	mutable uint8 low, high ;
	l = cvt [ 0 ] -(uint8) 48 ;
	h = cvt [ 1 ] -(uint8) 48 ;
	low = mapp [ l ] << 4 ;
	high = mapp [ h ] ;
	return(low | high) ;
}

composite AudioSample
{
	param 
		expression <int32> $SAMPLE_FFT : (int32)4096;
		expression <int32> $OVERLAP_FFT : (int32)1024;
	graph
	

		(stream<rstring data> WebSocketInject_2_out0) as WebSocketInject_2 =
		//(stream<list<uint16> data> WebSocketInject_2_out0) as WebSocketInject_2 =
		WebSocketInject()
		{
			param
				port : 8086 ;
			config
				placement : hostColocation("jettyStatus") ;
		}

		(stream<AudioDataType> Custom_7_out0 as DataOut ; stream<AudioCommandType>
			Custom_7_out1 as ControlOut) as WSSocketProcess =
			Custom(WebSocketInject_2_out0 as RawIn)
		{
			logic
				state :
				{
					mutable int32 messageCount = 0 ;
					list<uint8> resetOut = [ ] ;
					mutable list<uint8> out ;
					mutable BinaryDataType bdt ;
					mutable list<uint8> cvt ;
					mutable int32 hash1, hash2 ;
					mutable AudioDataType adt ;
					mutable AudioCommandType act ;
				}

				onTuple RawIn :
				{
					messageCount ++ ;
					out = resetOut ;
					if((length(data) != 0) &&(data [ 0 ] == "#"))
					{
						hash1 = findFirst(data, "#", 1) ;
						if(hash1 != - 1)
						{
							hash2 = findFirst(data, "#", hash1 + 1) ;
							if(hash2 != - 1)
							{
								adt.id = data [ 1 : hash1 ] ;
								adt.command = data [ hash1 + 1 : hash2 ] ;
								if((messageCount % 100) == 0)
								{
									appLog(Log.info, "messageCount:" +(rstring) messageCount + " id:" +
										adt.id + " command:" + adt.command + " length:" +(rstring)
										length(data)) ;
								}

								if(adt.command == "DATA")
								{
									cvt =(list<uint8>) convertToBlob(data [ hash2 + 1 : ]) ;
									for(int32 i in range(0, size(cvt), 2))
									{
										appendM(out, hexToInt(cvt [ i : i + 2 ])) ;
									}

									adt.outBlob =(blob) out ;
									adt.size = blobSize(adt.outBlob) ;
									adt.signalData = data [ hash2 + 1 : ] ;
									submit(adt, DataOut) ;
									return ;
								}

								act.command = adt.command ;
								act.id = adt.id ;
								act.commandData = data [ hash2 + 1 : ] ;
								//appLog(Log.info, "ID: " + act.id + " COMMMAND:" + act.command) ;
								if(act.command == "STOP")
								{
									submit(Sys.WindowMarker, DataOut) ;
								}
								// All commands other the 'DATA' are sent down the line. 
								submit(act, ControlOut) ;
								return ;
							}

						}

					}

					act.id = "*UNKNOWN ID*" ;
					act.command = "UNKNOWN MESSAGE" ;
					act.commandData = data ;
					appLog(Log.warn, "Unknown message received : " + act.commandData [ : 40
						]) ;
					submit(act, ControlOut) ;
				}

		}

		(stream<AudioSampleType> Custom_10_out0 as O) as SampleGenerator =
			Custom(Custom_7_out0 as I)
		{
			logic
				state :
				{
					mutable AudioSampleType audioSample ;
					mutable uint64 bufLen = 0 ;
					mutable uint8 hByte ;
					mutable uint8 lByte ;
					mutable int16 word ;
				}

				onTuple Custom_7_out0 :
				{
					assignFrom(audioSample, I) ;
					// bufLen = blobSize(I.outBlob);
					for(int32 idx in range((int32) 0,(int32) blobSize(I.outBlob),(int32) 2))
					{
						hByte = outBlob [ idx + 1 ] ;
						lByte = outBlob [ idx ] ;
						word =(int16)((int16) hByte << 8 |(int16) lByte) ;
						audioSample.sample =((float32) word /(float32) 32767.0) ;
						appTrc(Trace.trace,(rstring) hByte + ":" +(rstring) lByte + "::"
							+(rstring) word + "->" +(rstring) audioSample.sample) ;
						submit(audioSample, O) ;
					}
				}
				onPunct Custom_7_out0 : submit(Sys.WindowMarker, O) ;
		}

		(stream<AudioSample64Type> Functor_14_out0) as Functor_14 =
			Functor(Custom_10_out0)
		{
			output
				Functor_14_out0 : id = id, sample =(float64) Custom_10_out0.sample ;
		}
		// Window $SAMPLE_FFT with and $OVERLAP_FFT of 1024
		(stream<AudioSampleListType> StageRawSample_out0 as O) as StageRawSample =
			Custom(Functor_14_out0 as I)
		{
		logic state: {
				mutable map <rstring, list<float64>>stage; 
				list<float64>initFloat64 = []; 
				mutable AudioSampleListType send;
				
			}
			onTuple I: {
				if (!has(stage, I.id)) { // not yet added, add it.
					stage[I.id] = initFloat64;	
					clearM(stage[I.id]);		
				}
				appendM(stage[I.id], I.sample);
				if (size(stage[I.id]) >= $SAMPLE_FFT) {
					send.id = I.id;
					send.samples = stage[I.id];
					clearM(stage[I.id]);
					stage[I.id] = send.samples[$SAMPLE_FFT-$OVERLAP_FFT:];	
					submit(send, O);					
				}
			}
		}
		(stream<rstring id, list<complex64> complex> localPort as FFTout) as
			AudioFFT = FFT(StageRawSample_out0 as audioIn)
		{
			param
				inputTimeSeries : samples ;
				algorithm : realFFT ;
				useHamming : true ;
			output
				FFTout : complex = FFTAsComplex() ;
		}

		/**
		 *  Note abs(<Complex>) is  a^2+b^2=c^2 
		 *  Convert the complex to a magnitude. 
		 */
		(stream<AudioFFTAmpType> Custom_5_out0 as O) as AbsoluteFFT =
			Custom(localPort as I)
		{
			logic
				state :
				{
					mutable list<float64> ampList ;
					mutable int32 idx = 0 ;
				}

				onTuple I :
				{
					clearM(ampList) ;
					for(complex64 comp in I.complex)
					{
						appendM(ampList, abs(comp)) ;
					}

					submit({ id = id, ampFFT = ampList }, O) ;
				}

		}
		/**
		 * Generate full set of tiles, only move the the lowest 4 down the stream.
		 * Upper tiles have no data, maybe should have just done a slice. 
		 */
		(stream<AudioQuantileType> Tile_out0 as O) as Tile = Custom(Custom_5_out0 as
			I)
		{
			logic
				state :
				{
					mutable list<float64> stage = [ ] ;
					mutable float64 sliceTotal ;
					mutable uint32 sliceChunk ;
					mutable int32 sliceSize ;
					//				 	mutable int32 idx;

				}

				onTuple I :
				{
					clearM(stage) ;
					sliceTotal = sum(ampFFT) ;
					sliceSize = size(ampFFT) / 10 ;
					//for (float64 i in range(I.ampFFT));				
					for(int32 idx in range(0, size(ampFFT), sliceSize))
					{
						appendM(stage, sum(ampFFT [ idx : idx + sliceSize ]) / sliceTotal) ;
					}

					submit({ id = id, tile1 = stage [ 0 ], tile2 = stage [ 1 ], tile3 = stage
						[ 2 ], tile4 = stage [ 3 ] }, O) ;
				}
		}

		() as WebSocketSend_8 = WebSocketSend(Tile_out0 as inPort0Alias)
		{
			param
				port : 8087 ;
			config
				placement : hostColocation("jettyStatus") ;
		}

		() as WebSocketSend_13 = WebSocketSend(Filter_14_out0 as inPort0Alias)
		{
			param
				port : 8088 ;
			config
				placement : hostColocation("jettyStatus") ;
		}

		(stream<AudioSample64Type> Filter_14_out0) as Filter_14 =
			Filter(Functor_14_out0 as inPort0Alias)
		{
			logic
				state : mutable int64 idx = 0 ;
			param
				filter :(id [ 0 ] == "+") &&(idx ++ %(int64) 2) ==(int64) 0 ; // count pluses + 1

		}



}
